CONTEXT JustGegevens IN DUTCH
PURPOSE CONTEXT JustGegevens
{+Justitiële gegevens zijn limitatief gedefinieerd op grond van Artikel 7, tweede lid, Wjsg:
Toegang tot justitiële gegevens is uitsluitend voorbehouden aan personen die onder het beheer van de Onze Minister ressorteren of op grond van zijn instructie, behoudens een bij of krachtens de wet gegeven voorschrift.
Welke gegevens justitieel zijn wordt in hfst. 2 afdeling 1 van de Bjsg bepaald.
+}

INCLUDE "Persoongegevens.adl"  -- 
INCLUDE "Feitgegevens.adl"     -- 
INCLUDE "Zaakgegevens.adl"     -- 
INCLUDE "Retentie.adl"         -- 
INCLUDE "Wetten.adl"           -- 
INCLUDE "Justitieel.adl"       -- 
INCLUDE "Verstrekkingen.adl"   -- omdat we de structuur van de regels ten aanzien van verstrekkingen.
INCLUDE "BjsgArtikel4.adl"     -- voor de overtredingssoorten die als justitiële gegevens aangemerkt worden.
INCLUDE "Berichten.adl"        -- omdat we de structuur van de Nederlandse wetgeving nodig hebben.
INCLUDE "Uittreksel.adl"       -- definieert verstrekkingen ten behoeve van ketenpartners.
INCLUDE "JustGegevensPop.xlsx"  -- 

CONCEPT Sepot "Onder sepot wordt verstaan de beslissing van het openbaar ministerie tot niet vervolging of niet verdere vervolging van de zaak."

REPRESENT Tekst TYPE BIGALPHANUMERIC -- Temporary for prototyping


PATTERN "Bewijsmiddel"
CLASSIFY Beschikking ISA Document
RELATION bewijsmiddel[Feit*Document]  -- zie Artikel 3 en 5 Bjsg
MEANING "Documenten, die als justitiële of strafvorderlijke gegevens zijn opgenomen in het systeem, en als bewijs gelden voor enig feit, worden in het systeem aan dat feit gekoppeld."

ENDPATTERN


INTERFACE Zaken : '_SESSION' cRud BOX
    [ zaken    : V[SESSION*Zaak] cRud
      BOX [ Parketnummer : I cRud
          , tenlastelegging : procesVerbaal;strafbaar;kwalificatie
          ]
    ]

INTERFACE Rechtsgrond : I[RechtsGrond] BOX
    [ regeling   : regeling
    , artikel    : artikel
    , lid        : lid
    , sub        : sub
    , url        : url
    , bijlage    : bijlage
    , aanwijzing : aanwijzing
    ]

VIEW Zaak : Zaak ( parketnummer )

PURPOSE INTERFACE JustitieleGegevens
{+Om de justitiële gegevens van een specifieke zaak te bepalen hanteren we hetgeen in Artikel 7 Bjsg is vermeld.
De analyse is gebaseerd op BWBR0016544-geldend op 15-09-2017.
+}
INTERFACE JustitieleGegevens FOR JustidBeheerder : I[Zaak] cRud
BOX [ Beslissingen : beslissing - nietVervolgd                                -- Artikel 7 lid 1 sub a Bjsg
      BOX [ "bepalingen" : bepaling
          , "strafbepalingen" : strafbepaling
          , "voorwaarden" : voorwaarde
          ]
    , Parketnummer : parketnummer CRud                                        -- Artikel 7 lid 1 sub b Bjsg
    , Parket : parket                                                         -- Artikel 7 lid 2 sub a Bjsg
    , "Proces-verbaal" : procesVerbaal
      BOX [ "Ontvangen op parket" : ontvangstParket;voorgedaan                -- Artikel 7 lid 2 sub b Bjsg
          , "in behandeling Hoge Raad" : inBehandelingPG;voorgedaan           -- Artikel 7 lid 2 sub c Bjsg
          ]
    , "Strafbaar feit" : procesVerbaal;strafbaar;(I[Feit]/\tav;strafbepaling;strafbepaling~;tav~)
      BOX [ omschrijving : omschrijving                                       -- Artikel 7 lid 1 sub d Bjsg
          , kwalificatie : kwalificatie                                       -- Artikel 7 lid 1 sub d Bjsg
          , "maatschappelijke classificatie" : maatschappelijkeClassificatie  -- Artikel 7 lid 1 sub e Bjsg
          , datum : voorgedaan[Gebeurtenis*Datum]                             -- Artikel 7 lid 1 sub f Bjsg
          , periode : voorgedaan[Feit*Periode]                                -- Artikel 7 lid 1 sub f Bjsg
          , sepot : sepot                                                     -- Artikel 7 lid 1 sub g Bjsg
            BOX [ datum : datum
                , sepotcode : sepotcode
                , grond : grond
                , "gestelde voorwaarden" : voorwaarde
                , "aan alles voldaan per (datum)" : voldaan
                ]
          , "bij beschikking" : tav;I[Beschikking]                            -- Artikel 7 lid 1 sub h Bjsg
            BOX [ datum : datum
                , "opgelegde straffen, maatregelen en aanwijzingen" : strafbepaling
                , "onherroepelijk per (datum)" : onherroepelijk
                , "uitgevoerd per (datum)" : uitgevoerd
                , "Art. 10, 11, of 12 tot (datum)" : gemerkt101112
                ]
          ]
    , "Strafbepaling(en)" : procesVerbaal;strafbaar;tav;strafbepaling                               -- Artikel 7 lid 1 sub c Bjsg
    , "voorlopige maatregel op grond van de WED" : beslissing;(I[Beslissing]/\ogv;regeling;bwb;'BWBR0002063';(ogv;regeling;bwb)~);maatregel                           -- Artikel 7 lid 1 sub i Bjsg
      BOX [ datum : maatregel~;datum
          , aanduiding : aanduiding
          , beëindiging : beeindiging
          , verlenging : verlenging
          , wijziging : wijziging
          , intrekking : intrekking
          , opheffing : opheffing
          ]
    , "rechterlijke uitspraken" : beslissing;I[RechterlijkeUitspraak]          -- Artikel 7 lid 1 sub j Bjsg
      BOX [ gerecht : gerecht
          , datum : datum
          , kwalificatie : tav~;kwalificatie
          , strafbepalingen : strafbepaling
          , voorwaarden : voorwaarde
          , "onherroepelijk per (datum)" : onherroepelijk
          , "vermoedelijke einde van een proeftijd" : maatregel;eindeProeftijd
          , "Art. 10, 11, of 12 tot (datum)" : gemerkt101112
          ]
    , "ten uitvoer gelegde maatregelen" : beslissing;(I[RechterlijkeUitspraak]/\uitgevoerd;uitgevoerd~);maatregel  -- Artikel 7 lid 1 sub k Bjsg
      BOX [ "uitgevoerd per (datum)" : maatregel~;uitgevoerd
          , wijze : aanduiding
          , aangevangen : aanvang
          , beëindigd : beeindiging
          , invrijheidstelling : invrijheidstelling                           -- Artikel 7 lid 1 sub l Bjsg
          ]
    ]

ENDCONTEXT